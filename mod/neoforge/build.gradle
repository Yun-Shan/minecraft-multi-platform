plugins {
    alias(libs.plugins.shadow)
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge libs.neoforge.api

    modImplementation libs.architectury.neoforge

    common(project(path: ':common')) { transitive = false }
    common(project(path: ':mod:common', configuration: 'namedElements')) { transitive = false }

    shadowBundle project(path: ":common")
    shadowBundle project(path: ':mod:common', configuration: 'transformProductionNeoForge')
}

processResources {
    def props = [
            mod_id              : rootProject.mod_id,
            mod_name            : rootProject.mod_name,
            version             : version,
            minecraft_version   : libs.versions.minecraft.get(),
            neoforge_version    : libs.versions.neoforge.get().split('\\.')[0],
            architectury_version: libs.versions.architectury.get(),
            project_author      : rootProject.project_author.split(',').stream().map { "\"${it}\"" }.toList().join(", "),
            project_description : rootProject.project_description,
    ]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('META-INF/neoforge.mods.toml') {
        expand props
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile = shadowJar.archiveFile
}
